buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "kotlin"

    version = "2.0.0"
    ext {
        appName = "PolyrhythmMania"
        
        paintboxVersion = "0.6.0"
        useLocalPaintbox = true
    }
    
    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(11))
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://clojars.org/repo/" }
        maven { url "https://jitpack.io/" }
        maven { url "https://repo.eclipse.org/content/groups/releases/" }
    }
    
    dependencies {
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "11"
            freeCompilerArgs += "-opt-in=kotlin.ExperimentalUnsignedTypes"
        }
    }
}

project(":core") {
    dependencies {
        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-core:${paintboxVersion}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-core:${paintboxVersion}"
        }

        api("net.beadsproject:beads:3.2")
        implementation "com.badlogicgames.jlayer:jlayer:1.0.1-gdx"
        api "net.lingala.zip4j:zip4j:2.9.1"
        api "org.apache.httpcomponents:httpclient:4.5.13"
        api "space.earlygrey:shapedrawer:v2.5.0"
        api "com.github.JnCrMx:discord-game-sdk4j:0.5.5"
        api "com.twelvemonkeys.imageio:imageio-tga:$imageio_tga_version"
    }
}

project(":desktop") {
    dependencies {
        implementation project(":core")

        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-desktop:${paintboxVersion}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-desktop:${paintboxVersion}"
        }
    }
}