import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.johnrengelman:shadow:8.1.1"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "kotlin"

    version = "2.1.1"
    ext {
        appName = "PolyrhythmMania"
        
        paintboxVersion = "0.17.0"
        useLocalPaintbox = false
    }
    
    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(11))
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://clojars.org/repo/" }
        maven { url "https://jitpack.io/" }
        maven { url "https://repo.eclipse.org/content/groups/releases/" }
    }
    
    dependencies {
    }

    tasks.withType(KotlinCompilationTask).configureEach {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
            freeCompilerArgs.add("-opt-in=kotlin.ExperimentalUnsignedTypes")
        }
    }
}

project(":core") {
    dependencies {
        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-core:${paintboxVersion}"
            implementation "com.github.chrislo27:paintbox-ui:${paintboxVersion}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-core:${paintboxVersion}"
            implementation "com.github.chrislo27.paintbox:paintbox-ui:${paintboxVersion}"
        }

        implementation "net.beadsproject:beads:3.2"
        implementation "com.badlogicgames.jlayer:jlayer:1.0.1-gdx"
        implementation "io.dropwizard.metrics:metrics-core:4.2.19"
        implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"
        implementation "net.lingala.zip4j:zip4j:2.11.5"
        implementation "org.apache.httpcomponents:httpclient:4.5.13"
        implementation "space.earlygrey:shapedrawer:v2.6.0"
        implementation "com.github.JnCrMx:discord-game-sdk4j:v0.5.5"
        implementation "com.twelvemonkeys.imageio:imageio-tga:$imageio_tga_version"
    }
}

project(":desktop") {
    dependencies {
        implementation project(":core")

        if (useLocalPaintbox) {
            implementation "com.github.chrislo27:paintbox-desktop:${paintboxVersion}"
        } else {
            implementation "com.github.chrislo27.paintbox:paintbox-desktop:${paintboxVersion}"
        }
        
        implementation "com.badlogicgames.gdx:gdx-lwjgl3-glfw-awt-macos:${libgdx_version}"
    }
}